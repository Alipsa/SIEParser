/*
 * Gradle 9.3 build script
 *
 */
plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id("se.alipsa.nexus-release-plugin") version '2.0.1'
}

group = 'com.github.pernyfelt.sieparser'
version = '2.0-SNAPSHOT'

base {
    archivesName = 'sieparser'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.deprecation = true
    options.compilerArgs << "-Xlint:unchecked"
}

repositories {
    mavenCentral()
}

dependencies {
    // The production code uses the SLF4J logging API at compile time
    api 'org.slf4j:slf4j-api:2.0.16'

    // JAXB for SIE 5 XML support
    api 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'
    runtimeOnly 'org.glassfish.jaxb:jaxb-runtime:4.0.5'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.4'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'org.slf4j:slf4j-simple:2.0.16'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = 'SIEParser'
                description = 'A Java parser for SIE files that can read files of version 1 to 4 (including 4i)'
                artifactId = 'SieParser'
                url = 'http://github.com/perNyfelt/SIEParser'
                packaging = 'jar'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }
                inceptionYear = '2016'
                organization {
                    name = 'Alipsa HB'
                    url = 'http://www.alipsa.se'
                }
                developers {
                    developer {
                        id = 'perNyfelt'
                        name = 'Per Nyfelt'
                        organizationUrl = 'http://www.alipsa.se'
                    }
                }
                issueManagement {
                    system = 'github'
                    url = 'http://github.com/perNyfelt/SIEParser/issues'
                }
                scm {
                    url = 'http://github.com/perNyfelt/SIEParser'
                    connection = 'scm:git:http://github.com/perNyfelt/SIEParser.git'
                    developerConnection = 'scm:git:git@github.com:perNyfelt/SIEParser.git'
                }
            }
        }
    }
}

signing {
    if (project.properties['signing.keyId'] != null) {
        sign publishing.publications.maven
    } else {
        project.logger.debug("signing.keyId is not defined, skipping signing of artifacts...")
    }
}

nexusReleasePlugin {
    userName = project.ext.properties.sonatypeUsername
    password = project.ext.properties.sonatypePassword
    mavenPublication = publishing.publications.maven
}
