/*
 * Gradle 9.3 build script
 *
 */
plugins {
    id 'java-library'
    id 'signing'
    id 'maven-publish'
    id("se.alipsa.nexus-release-plugin") version '2.0.1'
    id "com.github.ben-manes.versions" version "0.53.0"
}

group = 'se.alipsa'
version = '2.0'

base {
    archivesName = 'sieparser'
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.deprecation = true
    options.compilerArgs << "-Xlint:unchecked"
}

repositories {
    mavenCentral()
}

dependencies {
    // JAXB for SIE 5 XML support
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.5'
    runtimeOnly 'org.glassfish.jaxb:jaxb-runtime:4.0.6'

    testImplementation 'org.bouncycastle:bcpkix-jdk18on:1.83'
    testImplementation 'org.junit.jupiter:junit-jupiter:6.0.3'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'org.slf4j:slf4j-simple:2.0.17'
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = 'SIEParser'
                description = 'A Java parser for SIE files (version 1 to 4 including 4i, and SIE 5 XML format)'
                artifactId = 'SieParser'
                url = 'https://github.com/Alipsa/SIEParser'
                packaging = 'jar'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://opensource.org/licenses/MIT'
                    }
                }
                inceptionYear = '2016'
                organization {
                    name = 'Alipsa HB'
                    url = 'http://www.alipsa.se'
                }
                developers {
                    developer {
                        id = 'perNyfelt'
                        name = 'Per Nyfelt'
                        organizationUrl = 'http://www.alipsa.se'
                    }
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/Alipsa/SIEParser/issues'
                }
                scm {
                    url = 'https://github.com/Alipsa/SIEParser'
                    connection = 'scm:git:https://github.com/Alipsa/SIEParser.git'
                    developerConnection = 'scm:git:git@github.com:Alipsa/SIEParser.git'
                }
            }
        }
    }
}

def isNonStable = { String v ->
    def stableKeyword = ['RELEASE','FINAL','GA'].any { v?.toUpperCase()?.contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    !stableKeyword && !(v ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    gradleReleaseChannel = 'current'
    rejectVersionIf {
        isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
    }
}

signing {
    if (project.properties['signing.keyId'] != null) {
        sign publishing.publications.maven
    } else {
        project.logger.debug("signing.keyId is not defined, skipping signing of artifacts...")
    }
}

nexusReleasePlugin {
    def sonatypeUsername = findProperty('sonatypeUsername')
    def sonatypePassword = findProperty('sonatypePassword')
    if (sonatypeUsername != null && sonatypePassword != null) {
        userName = sonatypeUsername
        password = sonatypePassword
    } else {
        project.logger.debug("sonatypeUsername/sonatypePassword are not defined, skipping release credential configuration...")
    }
    mavenPublication = publishing.publications.maven
}
